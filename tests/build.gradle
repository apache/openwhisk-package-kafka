apply plugin: 'scala'
apply plugin: 'eclipse'
compileTestScala.options.encoding = 'UTF-8'

evaluationDependsOn(':whisktests')

repositories {
  mavenCentral()
  flatDir {
    dirs 'libs'
  }
}

def commonConfiguration = {
  systemProperty 'testthreads', System.getProperty('testthreads', '1')
  systemProperty 'health_url', System.getProperty('health_url', '')
  systemProperty 'host', System.getProperty('host', '')
  systemProperty 'port', System.getProperty('port', '')
  systemProperty 'trigger.suffix', System.getProperty('trigger.suffix', '')
  testLogging {
    events "passed", "skipped", "failed"
    showStandardStreams = true
    exceptionFormat = 'full'
  }
  outputs.upToDateWhen { false } // force tests to run everytime
}

test {
  configure commonConfiguration
  exclude 'system/stress/**'
}

task testWithoutCredentials(type: Test) {
  configure commonConfiguration
}

task stress(type: Test) {
  configure commonConfiguration
  include 'system/stress/**'
}

task testHealth(type: Test) {
  configure commonConfiguration
  systemProperty 'test.router', 'true'
  include 'system/health/**'
}

dependencies {
  compile "org.scala-lang:scala-library:${gradle.scala.version}"
  compile 'com.ibm.messagehub:messagehub.login:1.0.0'
  compile project(':whisktests')
  compile project(':whisktests').sourceSets.test.output
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.additionalParameters = gradle.scala.compileFlags
}
